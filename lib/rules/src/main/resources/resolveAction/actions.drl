package at.jku.dke.inga.rules

// ------------------------------------ GLOBAL --------------------------------------
global org.apache.logging.log4j.Logger LOGGER

// ------------------------------------ IMPORT --------------------------------------
import org.apache.logging.log4j.Logger
import java.util.Map
import at.jku.dke.inga.rules.models.ResolveActionModel
import at.jku.dke.inga.shared.EventNames
import at.jku.dke.inga.shared.operations.Operation

// ---------------------------------- FUNCTIONS -------------------------------------
function boolean isActionNumber(String s){
   try {
       Integer.valueOf(s);
       return true;
   } catch( Exception e ){
       return false;
   }
}

function int toActionNumber(String s){
   try {
       return Integer.valueOf(s);
   } catch(Exception e){
       return -1;
   }
}

// ------------------------------------ RULES ---------------------------------------
rule "Resolve Action"
    agenda-group "resolve-action"
when
    $model : ResolveActionModel(isActionNumber(userInput), possibleOperations.containsKey(toActionNumber(userInput)))
then
    LOGGER.debug("Action resolved");
    insert($model.getPossibleOperations().get(toActionNumber($model.getUserInput())).getEventName());
end

rule "Invalid Action Number"
    agenda-group "resolve-action"
when
    $model : ResolveActionModel(!isActionNumber(userInput) || !possibleOperations.containsKey(toActionNumber(userInput)))
then
    LOGGER.debug("Invalid Input found");
    insert(EventNames.INVALID_INPUT);
end