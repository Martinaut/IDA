package at.jku.dke.inga.rules

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.inga.shared.EventNames
import at.jku.dke.inga.shared.models.NonComparativeAnalysisSituation
import at.jku.dke.inga.shared.operations.Operation
import at.jku.dke.inga.rules.models.OperationDisplayDeterminationServiceModel

// ------------------------------ MEASURE OPERATIONS --------------------------------
rule "'Add Measure' - if at least one measure is available in the cube for selection"
    agenda-group "operations-display-determination"
when
    $model : OperationDisplayDeterminationServiceModel(notSelectedMeasures.isEmpty() == false)
then
    insert(new Operation(EventNames.NAVIGATE_MEASURE_ADD, $model.getLocale(), 10));
end

rule "'Refocus Measure' - if at least one measure is available"
    agenda-group "operations-display-determination"
when
    $as : NonComparativeAnalysisSituation(measures.isEmpty() == false)
    $model : OperationDisplayDeterminationServiceModel(notSelectedMeasures.isEmpty() == false, analysisSituation == $as)
then
    insert(new Operation(EventNames.NAVIGATE_MEASURE_REFOCUS, $model.getLocale(), 11));
end

rule "'Drop Measure' - if at least one measure is selected and one available"
    agenda-group "operations-display-determination"
when
    $as : NonComparativeAnalysisSituation(measures.isEmpty() == false)
    $model : OperationDisplayDeterminationServiceModel(analysisSituation == $as)
then
    insert(new Operation(EventNames.NAVIGATE_MEASURE_DROP, $model.getLocale(), 14));
end