package at.jku.dke.inga.rules

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.inga.rules.models.ValueDeterminationServiceModel
import at.jku.dke.inga.rules.helpers.UserInput
import at.jku.dke.inga.rules.results.ConfidenceResult
import at.jku.dke.inga.rules.results.StringValueConfidenceResult
import at.jku.dke.inga.rules.results.TwoStringValueConfidenceResult
import at.jku.dke.inga.shared.display.Display
import at.jku.dke.inga.shared.display.ListDisplay
import at.jku.dke.inga.shared.display.TwoListDisplay
import at.jku.dke.inga.shared.EventNames

// ------------------------------------ RULES ---------------------------------------
rule "Resolve Value - ListDisplay"
    agenda-group "value-determination"
    salience 100
when
    $display : ListDisplay()
    $model : ValueDeterminationServiceModel(UserInput.isNumber(userInput) &&
                                            UserInput.toInteger($model.getUserInput()) > 0 &&
                                            UserInput.toInteger($model.getUserInput()) <= $display.data.size(), displayData == $display)
then
    insert(new StringValueConfidenceResult($display.getData().get(UserInput.toInteger($model.getUserInput()) - 1).getDisplayableId(), 1.0));
end

rule "Resolve Value - TwoListDisplay"
    agenda-group "value-determination"
    salience 100
when
    $display : TwoListDisplay()
    $model : ValueDeterminationServiceModel(UserInput.isTwoNumberSelection(userInput) &&
                                            UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getLeft() > 0 &&
                                            UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getRight() > 0 &&
                                            UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getLeft() <= $display.dataLeft.size() &&
                                            UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getRight() <= $display.dataRight.size(), displayData == $display)
then
    insert(new TwoStringValueConfidenceResult(
                    $display.getDataLeft().get(UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getLeft() - 1).getDisplayableId(),
                    $display.getDataRight().get(UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getRight() - 1).getDisplayableId(),
                     1.0));
end

rule "Invalid Operation Number - ListDisplay"
    agenda-group "value-determination"
    salience 2
when
    $display : Display()
    $model : ValueDeterminationServiceModel(displayData == $display)
    not ConfidenceResult()
then
    insert(new StringValueConfidenceResult(EventNames.INVALID_INPUT, 1.0));
end