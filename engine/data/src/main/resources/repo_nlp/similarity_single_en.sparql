PREFIX : <http://www.ontotext.com/graphdb/similarity/>
PREFIX inst: <http://www.ontotext.com/graphdb/similarity/instance/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sim: <http://dke.jku.at/ida/similarity#>

SELECT ?cube ?dimension ?element ?type (MAX(?score) AS ?score)
WHERE {
    {
        SELECT ?cube ?dimension ?element ?type ?documentID ?score1
		WHERE {
		    GRAPH sim:wordnet-scores-en {
		        ?mapping rdf:type     sim:Mapping ;
                         sim:from     ?element ;
                         sim:to       ?documentID ;
                         sim:hasScore ?score1 ;
                         sim:inCube   ?cube ;
                         sim:fromType ?type .
                OPTIONAL { ?mapping sim:inDim ?dimension }
		    }
		}
    }
    {
        SELECT ?documentID ?score2 {
            ?search a inst:wordnet ;
                :searchTerm "###TERM###" ;
                :searchParameters "" ;
                :documentResult ?result .
            ?result :value ?documentID ;
                    :score ?score2 .
        }
    }
    BIND((?score1 * ?score2) AS ?score)
} GROUP BY ?cube ?dimension ?element ?type
ORDER BY DESC(?score)