PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX qbx: <http://dke.jku.at/inga/cubes#>

SELECT ?dimension ?dimensionLabel ?element ?label ?description
WHERE {
    { ?element qbx:over ?level . }
    UNION
    {
        ?pred qbx:over ?level .
        ?element qbx:conjunct ?pred .
    }

    {
        SELECT DISTINCT ?dimension ?level
        WHERE {
            {
                <###CUBE###> qbx:dimension ?dimension .
    			?dimension qbx:hasHierarchy ?hier .
    			?hs qbx:inHierarchy ?hier ;
    				qbx:childLevel ?level .
            } UNION {
                <###CUBE###> qbx:dimension ?dimension .
    			?dimension qbx:hasHierarchy ?hier .
    			?hs qbx:inHierarchy ?hier ;
    				qbx:parentLevel ?level .
            }
        }
    }

    ?element rdfs:label ?label .
    FILTER (lang(?label) = "###LANG###") .
    OPTIONAL {
        ?element rdfs:comment ?description .
    	FILTER (lang(?description) = "###LANG###") .
    }

    ?dimension rdfs:label ?dimensionLabel .
    FILTER (lang(?dimensionLabel) = "###LANG###") .

    FILTER (?element NOT IN (###NOTIN###)) .
}
ORDER BY ?dimensionLabel ?label