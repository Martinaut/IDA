PREFIX qbx: <http://dke.jku.at/inga/cubes#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?dimension ?dimensionLabel ?element ?label ?description
WHERE {
    { ?element qbx:over ?level . }
    UNION
    {
        ?pred qbx:over ?level .
        ?element qbx:conjunct ?pred .
    }
    FILTER (?element IN (###IN###)) .

    {
        SELECT ?dimension ?level
        WHERE {
            {
    			?dimension qbx:hasHierarchy ?hier .
    			?hs qbx:inHierarchy ?hier ;
    				qbx:childLevel ?level .
            } UNION {
    			?dimension qbx:hasHierarchy ?hier .
    			?hs qbx:inHierarchy ?hier ;
    				qbx:parentLevel ?level .
            }
        }
    }

    ?element rdfs:label ?label .
    FILTER (lang(?label) = "###LANG###") .
    OPTIONAL {
        ?element rdfs:comment ?description .
    	FILTER (lang(?description) = "###LANG###") .
    }

    ?dimension rdfs:label ?dimensionLabel .
    FILTER (lang(?dimensionLabel) = "###LANG###") .
}
ORDER BY ?dimensionLabel ?label