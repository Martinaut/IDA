PREFIX qbx: <http://dke.jku.at/inga/cubes#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?dimension ?dimensionLabel ?element ?label ?description
WHERE {
    VALUES (?dimension) { (<###DIMENSION###>) }
    {
        SELECT ?dimension ?element
        WHERE {
            <###DIMENSION###> qbx:hasHierarchy ?hier .
            ?hs qbx:inHierarchy ?hier ;
                qbx:childLevel <###LEVEL###> ;
                qbx:parentLevel ?element .
        }
    }
    UNION
    {
        SELECT ?dimension ?element
        WHERE {
            <###DIMENSION###> qbx:hasHierarchy ?hier .
            ?hs qbx:inHierarchy ?hier ;
                qbx:childLevel <###LEVEL###> ;
                qbx:parentLevel ?parent .
            ?parent (^qbx:childLevel/qbx:parentLevel)+ ?element .
        }
    }

    ?element rdfs:label ?label .
    FILTER (lang(?label) = "###LANG###") .
    OPTIONAL {
        ?element rdfs:comment ?description .
        FILTER (lang(?description) = "###LANG###") .
    }

    ?dimension rdfs:label ?dimensionLabel .
    FILTER (lang(?dimensionLabel) = "###LANG###") .
}
ORDER BY ?dimensionLabel ?label