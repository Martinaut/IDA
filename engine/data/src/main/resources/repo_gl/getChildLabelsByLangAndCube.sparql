PREFIX qbx: <http://dke.jku.at/inga/cubes#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?dimension ?dimensionLabel ?element ?label ?description
WHERE {
    {
        SELECT ?dimension ?element
        WHERE {
            <###CUBE###> qbx:dimension ?dimension .
            ?dimension qbx:hasHierarchy ?hier .
            ?hs qbx:inHierarchy ?hier ;
                qbx:childLevel  ?element;
                qbx:parentLevel <###LEVEL###> .
        }
    }
    UNION
    {
        SELECT ?dimension ?element
        WHERE {
            <###CUBE###> qbx:dimension ?dimension .
            ?dimension qbx:hasHierarchy ?hier .
            ?hs qbx:inHierarchy ?hier ;
                qbx:childLevel ?child ;
                qbx:parentLevel <###LEVEL###> .
            ?child (^qbx:parentLevel/qbx:childLevel)+ ?element .
        }
    }

    ?element rdfs:label ?label .
    FILTER (lang(?label) = "###LANG###") .
    OPTIONAL {
        ?element rdfs:comment ?description .
        FILTER (lang(?description) = "###LANG###") .
    }

    ?dimension rdfs:label ?dimensionLabel .
    FILTER (lang(?dimensionLabel) = "###LANG###") .
}
