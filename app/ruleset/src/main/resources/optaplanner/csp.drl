package optaplanner

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.ida.csp.domain.AnalysisSituation
import at.jku.dke.ida.app.ruleset.helpers.CSPRuleHelpers
import org.optaplanner.core.api.score.buildin.hardsoftbigdecimal.HardSoftBigDecimalScoreHolder
import java.math.BigDecimal
import at.jku.dke.ida.app.ruleset.helpers.CSPRuleHelpers

// ------------------------------------ GLOBAL --------------------------------------
global HardSoftBigDecimalScoreHolder scoreHolder;

// ------------------------------------ RULES ---------------------------------------
// ### EMPTY VALUES ###
rule "No cube set"
when
    AnalysisSituation(cube == null || cube.isEmpty())
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "No measure set"
when
    AnalysisSituation(measures == null)
then
    scoreHolder.addSoftConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

// ### SAME CUBE ###
rule "All measures in same cube"
when
    AnalysisSituation(!CSPRuleHelpers.allInCube(cube, measures))
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "All levels in same cube"
when
    AnalysisSituation(!CSPRuleHelpers.allInCube(cube, granularityLevels))
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "All slice conditions in same cube"
when
    AnalysisSituation(!CSPRuleHelpers.allInCube(cube, sliceConditions))
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "All bmcs in same cube"
when
    AnalysisSituation(!CSPRuleHelpers.allInCube(cube, baseMeasureConditions))
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "All filters in same cube"
when
    AnalysisSituation(!CSPRuleHelpers.allInCube(cube, filterConditions))
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "Maximum one granularity level per dimesion"
when
    AnalysisSituation(!CSPRuleHelpers.maximumOneElementPerDimension(granularityLevels))
then
    scoreHolder.addHardConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "Maximum one assignment for a term"
when
    $as : AnalysisSituation()
    eval(CSPRuleHelpers.multipleAssignmentsPerTerm($as))
then
    scoreHolder.addSoftConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

rule "No duplicates"
when
    $as : AnalysisSituation()
    eval(CSPRuleHelpers.containsDuplicates($as))
then
    scoreHolder.addSoftConstraintMatch(kcontext, CSPRuleHelpers.MINUS_ONE);
end

// ### SCORE ###
rule "Similarity Score"
when
    $as : AnalysisSituation(cube != null)
then
    scoreHolder.addSoftConstraintMatch(kcontext, $as.getScore());
end