package app_rules.operation

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.ida.rules.interfaces.OperationServiceModel
import at.jku.dke.ida.shared.Event
import at.jku.dke.ida.shared.operations.Operation
import at.jku.dke.ida.rules.results.EventConfidenceResult
import at.jku.dke.ida.app.ruleset.helpers.UserInput
import at.jku.dke.ida.app.ruleset.interception.models.OperationModel

// ------------------------------------ RULES ---------------------------------------
rule "Resolve Operation"
    agenda-group "operation-determination"
    salience 100
when
    $model : OperationServiceModel(UserInput.isNumber(userInput), possibleOperations.containsKey(UserInput.toInteger(userInput)))
then
    insert(new EventConfidenceResult($model.getPossibleOperations().get(UserInput.toInteger($model.getUserInput())).getEvent(), 1.0));
end

rule "Resolve Operation Similarity"
    agenda-group "operation-determination"
    salience 100
when
    $model : OperationModel(topSimilarity != null, topSimilarity.getScore() >= 0.4)
    eval($model.getTopSimilarity().getElement() instanceof Operation)
then
    insert(new EventConfidenceResult(((Operation) $model.getTopSimilarity().getElement()).getEvent(), $model.getTopSimilarity().getScore(), $model.getTopSimilarity().getTerm().getText()));
end

rule "Invalid Operation Similarity"
    agenda-group "operation-determination"
    salience 2
when
    $model : OperationModel(topSimilarity == null)
    not EventConfidenceResult()
then
    insert(new EventConfidenceResult(Event.INVALID_INPUT, 1.0));
end

rule "Invalid Operation Number"
    agenda-group "operation-determination"
    salience 1
when
    $model : OperationServiceModel(!UserInput.isNumber(userInput) || !possibleOperations.containsKey(UserInput.toInteger(userInput)))
    not EventConfidenceResult()
then
    insert(new EventConfidenceResult(Event.INVALID_INPUT, 1.0));
end

rule "Too low Operation Similarity"
    agenda-group "operation-determination"
    salience 2
when
    $model : OperationModel(topSimilarity != null, topSimilarity.getScore() < 0.4)
    not EventConfidenceResult()
then
    insert(new EventConfidenceResult(Event.INVALID_INPUT, 1.0));
end