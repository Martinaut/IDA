package core_rules.value_display

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.ida.shared.Event
import at.jku.dke.ida.shared.models.NonComparativeAnalysisSituation
import at.jku.dke.ida.shared.models.DimensionQualification
import at.jku.dke.ida.shared.display.ListDisplay
import at.jku.dke.ida.shared.display.TwoListDisplay
import at.jku.dke.ida.rules.interfaces.ValueDisplayServiceModel
import at.jku.dke.ida.app.ruleset.helpers.Constants
import at.jku.dke.ida.app.ruleset.helpers.QueryHelper

// ------------------------------------ RULES ---------------------------------------
rule "Add Dice Node - Select Dimension"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == Event.NAVIGATE_DICE_NODE_MOVE_DOWN, analysisSituation == $as, additionalDataContainsKey(Constants.ADD_DATA_DIMENSION) == false)
then
    insert(new ListDisplay(
        "addDNDimension",
        $model.getLocale(),
        QueryHelper.getDimensionsWhereAddDicePossible($model)));
end
rule "Add Dice Node - Select Level"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == Event.NAVIGATE_DICE_NODE_MOVE_DOWN,
                                      analysisSituation == $as,
                                      additionalDataContainsKey(Constants.ADD_DATA_DIMENSION),
                                      additionalDataContainsKey(Constants.ADD_DATA_LEVEL) == false)
then
    insert(new ListDisplay(
        "addDNLevel",
        $model.getLocale(),
        QueryHelper.getLevelsWhereAddDicePossible($model)));
end
rule "Add Dice Node - Not selected level member"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == Event.NAVIGATE_DICE_NODE_MOVE_DOWN,
                                      analysisSituation == $as,
                                      additionalDataContainsKey(Constants.ADD_DATA_DIMENSION),
                                      additionalDataContainsKey(Constants.ADD_DATA_LEVEL))
then
    insert(new ListDisplay(
        "addDN",
        $model.getLocale(),
        QueryHelper.getLevelMembersToAdd($model)));
end

rule "Drop Dice Node - Select Dimension"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == Event.NAVIGATE_DICE_NODE_MOVE_UP, analysisSituation == $as)
then
    insert(new ListDisplay(
        "dropDN",
        $model.getLocale(),
        QueryHelper.getDimensionsWhereDropDicePossible($model)));
end