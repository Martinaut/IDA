package core_rules.value_display

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.inga.shared.EventNames
import at.jku.dke.inga.shared.models.NonComparativeAnalysisSituation
import at.jku.dke.inga.shared.models.DimensionQualification
import at.jku.dke.inga.shared.display.ListDisplay
import at.jku.dke.inga.shared.display.TwoListDisplay
import at.jku.dke.inga.rules.models.ValueDisplayServiceModel
import at.jku.dke.inga.app.ruleset.helpers.Constants
import at.jku.dke.inga.app.ruleset.helpers.QueryHelper

// ------------------------------------ RULES ---------------------------------------
rule "Roll Up - Select Dimension"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_GL_ROLL_UP, analysisSituation == $as, additionalDataContainsKey(Constants.ADD_DATA_DIMENSION) == false)
then
    insert(new ListDisplay(
        "rollUpDimension",
        $model.getLocale(),
        $model.getGranularityLevelRepository().getDimensionsWhereRollUpPossible($model.getLanguage(), $as.getCube(), $as.getDimensionQualifications())));
end

rule "Roll Up - All parent levels"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_GL_ROLL_UP, analysisSituation == $as, additionalDataContainsKey(Constants.ADD_DATA_DIMENSION))
then
    insert(new ListDisplay(
        "rollUp",
        $model.getLocale(),
        QueryHelper.getParentLevelLabelsByLangAndDimension($model)));
end

rule "Drill Down - Select Dimension"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_GL_DRILL_DOWN, analysisSituation == $as, additionalDataContainsKey(Constants.ADD_DATA_DIMENSION) == false)
then
    insert(new ListDisplay(
        "drillDownDimension",
        $model.getLocale(),
        $model.getGranularityLevelRepository().getDimensionsWhereDrillDownPossible($model.getLanguage(), $as.getCube(), $as.getDimensionQualifications())));
end

rule "Drill Down - All child levels"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_GL_DRILL_DOWN, analysisSituation == $as, additionalDataContainsKey(Constants.ADD_DATA_DIMENSION))
then
    insert(new ListDisplay(
        "drillDown",
        $model.getLocale(),
        QueryHelper.getChildLevelLabelsByLangAndDimension($model)));
end