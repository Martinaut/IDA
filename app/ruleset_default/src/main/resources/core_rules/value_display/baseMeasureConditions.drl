package core_rules.value_display

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.inga.shared.EventNames
import at.jku.dke.inga.shared.models.NonComparativeAnalysisSituation
import at.jku.dke.inga.shared.display.ListDisplay
import at.jku.dke.inga.shared.display.TwoListDisplay
import at.jku.dke.inga.rules.models.ValueDisplayServiceModel

// ------------------------------------ RULES ---------------------------------------
rule "Add base measure condition - Select all base measure conditions, except the already selected ones"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_BMC_NARROW, analysisSituation == $as)
then
    insert(new ListDisplay(
        "addBMC",
        $model.getLocale(),
        $model.getBaseMeasurePredicateRepository().getLabelsByLangAndCubeWithExclusions($model.getLanguage(), $as.getCube(), $as.getBaseMeasureConditions())));
end

rule "Drop base measure condition - Select all selected base measure conditions"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_BMC_BROADEN, analysisSituation == $as)
then
    insert(new ListDisplay(
        "dropBMC",
        $model.getLocale(),
        $model.getBaseMeasurePredicateRepository().getLabelsByLangAndIris($model.getLanguage(), $as.getBaseMeasureConditions())));
end

rule "Refocus base measure condition - Select all selected base measure conditions and all not selected base measure conditions"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_BMC_BROADEN, analysisSituation == $as)
then
    insert(new TwoListDisplay(
        "refocusBMC",
        $model.getLocale(),
        $model.getBaseMeasurePredicateRepository().getLabelsByLangAndIris($model.getLanguage(), $as.getBaseMeasureConditions()),
        $model.getBaseMeasurePredicateRepository().getLabelsByLangAndCubeWithExclusions($model.getLanguage(), $as.getCube(), $as.getBaseMeasureConditions())));
end