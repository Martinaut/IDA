package core_rules.value

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.ida.rules.interfaces.ValueServiceModel
import at.jku.dke.ida.rules.results.ConfidenceResult
import at.jku.dke.ida.rules.results.EventConfidenceResult
import at.jku.dke.ida.rules.results.DisplayableConfidenceResult
import at.jku.dke.ida.rules.results.DualDisplayableConfidenceResult
import at.jku.dke.ida.shared.display.Display
import at.jku.dke.ida.shared.display.ListDisplay
import at.jku.dke.ida.shared.display.TwoListDisplay
import at.jku.dke.ida.shared.Event
import at.jku.dke.ida.app.ruleset.helpers.UserInput
import at.jku.dke.ida.app.ruleset.interception.models.ValueModel

// ------------------------------------ RULES ---------------------------------------
rule "Resolve Value - ListDisplay"
    agenda-group "value-determination"
    salience 100
when
    $display : ListDisplay()
    $model : ValueServiceModel(UserInput.isNumber(userInput) &&
                                UserInput.toInteger($model.getUserInput()) > 0 &&
                                UserInput.toInteger($model.getUserInput()) <= $display.data.size(), displayData == $display)
then
    insert(new DisplayableConfidenceResult($display.getData().get(UserInput.toInteger($model.getUserInput()) - 1), 1.0));
end

rule "Resolve Value - TwoListDisplay"
    agenda-group "value-determination"
    salience 100
when
    $display : TwoListDisplay()
    $model : ValueServiceModel(UserInput.isTwoNumberSelection(userInput) &&
                                UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getLeft() > 0 &&
                                UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getRight() > 0 &&
                                UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getLeft() <= $display.dataLeft.size() &&
                                UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getRight() <= $display.dataRight.size(), displayData == $display)
then
    insert(new DualDisplayableConfidenceResult(
                    $display.getDataLeft().get(UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getLeft() - 1),
                    $display.getDataRight().get(UserInput.toTwoNumberSelectionInteger($model.getUserInput()).getRight() - 1),
                     1.0));
end

rule "Resolve Value - Similarity"
    agenda-group "value-determination"
    salience 100
when
    $model : ValueModel(topSimilarity != null)
then
    insert(new DisplayableConfidenceResult($model.getTopSimilarity().getElement(), $model.getTopSimilarity().getScore()));
end
// TODO twolist similarity??

rule "Invalid Value Similarity"
    agenda-group "value-determination"
    salience 3
when
    $model : ValueModel(topSimilarity == null)
    not ConfidenceResult()
then
    insert(new EventConfidenceResult(Event.INVALID_INPUT, 1.0));
end

rule "Invalid Value Number - ListDisplay"
    agenda-group "value-determination"
    salience 2
when
    $display : Display()
    $model : ValueServiceModel(displayData == $display)
    not ConfidenceResult()
then
    insert(new EventConfidenceResult(Event.INVALID_INPUT, 1.0));
end