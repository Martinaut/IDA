package value_display

// ------------------------------------ IMPORT --------------------------------------
import at.jku.dke.inga.shared.EventNames
import at.jku.dke.inga.shared.models.NonComparativeAnalysisSituation
import at.jku.dke.inga.shared.display.ListDisplay
import at.jku.dke.inga.shared.display.TwoListDisplay
import at.jku.dke.inga.rules.models.ValueDisplayServiceModel

// ------------------------------------ RULES ---------------------------------------
rule "Add Measure - Select all measures, except the already selected ones"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_MEASURE_ADD, analysisSituation == $as)
then
    insert(new ListDisplay(
        "addMeasure",
        $model.getLocale(),
        $model.getAggregateMeasureRepository().getLabelsByLangAndCubeWithExclusions($model.getLanguage(), $as.getCube(), $as.getMeasures())));
end

rule "Drop Measure - Select all selected Measures"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_MEASURE_DROP, analysisSituation == $as)
then
    insert(new ListDisplay(
        "dropMeasure",
        $model.getLocale(),
        $model.getAggregateMeasureRepository().getLabelsByLangAndIris($model.getLanguage(), $as.getMeasures())));
end

rule "Refocus Measure - Select all selected Measures and all not selected measures"
    agenda-group "value-display-determination"
when
    $as : NonComparativeAnalysisSituation()
    $model : ValueDisplayServiceModel(operation == EventNames.NAVIGATE_MEASURE_REFOCUS, analysisSituation == $as)
then
    insert(new TwoListDisplay(
        "refocusMeasure",
        $model.getLocale(),
        $model.getAggregateMeasureRepository().getLabelsByLangAndIris($model.getLanguage(), $as.getMeasures()),
        $model.getAggregateMeasureRepository().getLabelsByLangAndCubeWithExclusions($model.getLanguage(), $as.getCube(), $as.getMeasures())));
end