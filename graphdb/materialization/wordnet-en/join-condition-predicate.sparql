PREFIX : <http://www.ontotext.com/graphdb/similarity/>
PREFIX inst: <http://www.ontotext.com/graphdb/similarity/instance/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX qbx: <http://dke.jku.at/inga/cubes#>
PREFIX sim: <http://dke.jku.at/ida/similarity#>

INSERT {
    GRAPH sim:wordnet-scores-en {
        ?mapping rdf:type sim:Mapping ;
                 sim:inCube   ?cube ;
                 sim:from     ?element ;
                 sim:to       ?documentID ;
                 sim:hasScore ?score ;
                 sim:fromType qbx:ComparativeMeasure ;
                 sim:partOf   ?part .
    }
}
WHERE
{
    SELECT ?mapping ?cube ?element ?documentID ?score ?part
    WHERE {
        ?search a inst:wordnet ;
                :searchTerm         ?filterText ;
                :searchParameters   "" ;
                :documentResult     ?result .
        ?result :value ?documentID ;
                :score ?score .

        {
            SELECT DISTINCT ?cube ?element ?filterText ?part
            WHERE {
                ?element rdf:type qbx:JoinConditionPredicate ;
                         qbx:overLevelInSetOfInterest ?lvl ;
                         rdfs:label ?filterText.

                ?lvl ^(qbx:parentLevel|qbx:childLevel)/qbx:inHierarchy/^qbx:hasHierarchy/^qbx:dimension ?cube .

                FILTER(isLiteral(?filterText)) .
                FILTER(lang(?filterText) = "en") .

                BIND('setOfInterest' AS ?part)
            }
        }
        UNION
        {
            SELECT DISTINCT ?cube ?element ?filterText ?part
            WHERE {
                ?element rdf:type qbx:JoinConditionPredicate ;
                         qbx:overLevelInSetOfComparison ?lvl ;
                         rdfs:label ?filterText.

                ?lvl ^(qbx:parentLevel|qbx:childLevel)/qbx:inHierarchy/^qbx:hasHierarchy/^qbx:dimension ?cube .

                FILTER(isLiteral(?filterText)) .
                FILTER(lang(?filterText) = "en") .

                BIND('setOfComparison' AS ?part)
            }
        }
        BIND(IRI(CONCAT("sim:mapping_", STRUUID())) AS ?mapping)
    }
}