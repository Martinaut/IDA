PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX qbx: <http://dke.jku.at/inga/cubes#>
PREFIX sim: <http://dke.jku.at/ida/similarity#>

INSERT {
    GRAPH sim:cube-assignment {
        ?mapping rdf:type sim:CubeAssignment ;
                 sim:cube ?cube ;
                 sim:from ?element ;
                 sim:partOf ?part.
    }
}
WHERE
{
    {
        SELECT ?mapping ?cube ?element ?part
        WHERE {
            ?element rdf:type qbx:JoinConditionPredicate ;
                     qbx:overLevelInSetOfInterest ?lvl .

            ?lvl ^(qbx:parentLevel|qbx:childLevel)/qbx:inHierarchy/^qbx:hasHierarchy/^qbx:dimension ?cube .

            BIND('setOfInterest' AS ?part)
            BIND(IRI(CONCAT("sim:mapping_", STRUUID())) AS ?mapping)
        }
    }
    UNION
    {
        SELECT ?mapping ?cube ?element ?part
        WHERE {
            ?element rdf:type qbx:JoinConditionPredicate ;
                     qbx:overLevelInSetOfComparison ?lvl .

            ?lvl ^(qbx:parentLevel|qbx:childLevel)/qbx:inHierarchy/^qbx:hasHierarchy/^qbx:dimension ?cube .

            BIND('setOfComparison' AS ?part)
            BIND(IRI(CONCAT("sim:mapping_", STRUUID())) AS ?mapping)
        }
    }
}