PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX wordnet: <http://wordnet-rdf.princeton.edu/ontology#>
PREFIX sim: <http://dke.jku.at/ida/similarity#>
PREFIX : <http://www.ontotext.com/graphdb/similarity/>
PREFIX inst: <http://www.ontotext.com/graphdb/similarity/instance/>
PREFIX pred: <http://www.ontotext.com/graphdb/similarity/psi/>

INSERT {
	inst:wordnet :createIndex "-termweight idf" ;
		:analyzer "org.apache.lucene.analysis.en.EnglishAnalyzer" ;
		:documentID ?documentID .
		?documentID :documentText ?documentText .
} WHERE {
	SELECT DISTINCT ?documentID ?documentText WHERE {
	    {
	        SELECT DISTINCT ?term ?text WHERE {
	            GRAPH sim:wordnet {
	                ?term ontolex:canonicalForm ?canonicalForm .
	                ?term wordnet:partOfSpeech wordnet:noun .
	                ?term ontolex:sense ?lexicalizedSense .
	                ?lexicalizedSense ontolex:isLexicalizedSenseOf ?sense .
	                {
	                    ?canonicalForm ontolex:writtenRep ?text .
	                } UNION {
	                    ?sense wordnet:definition ?definition .
	                    ?definition rdf:value ?text .
	                }
	            }
	        }
	    } UNION {
	        SELECT DISTINCT ?term ?text WHERE {
	            GRAPH sim:wordnet {
	                ?term ontolex:canonicalForm?canonicalForm .
	                ?term wordnet:partOfSpeech wordnet:noun .
	                ?term ontolex:sense ?lexicalizedSense .
	                ?lexicalizedSense ontolex:isLexicalizedSenseOf ?sense .
	                ?sense wordnet:hyponym ?hyponym .
	                {
	                    ?hyponymLexicalizedSense ontolex:isLexicalizedSenseOf ?hyponym .
	                    ?hyponymTerm ontolex:sense ?hyponymLexicalizedSense .
	                    ?hyponymTerm ontolex:canonicalForm ?hyponymCanonicalForm .
	                    ?hyponymCanonicalForm  ontolex:writtenRep ?text .
	                } UNION {
	                    ?hyponym wordnet:definition ?hyponymDefinition .
	                    ?hyponymDefinition rdf:value ?text .
	                }
	            }
	        }
	    }
	    BIND(?term AS ?documentID)
	    BIND(?text AS ?documentText)
	}
}